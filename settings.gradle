dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
        mavenLocal()
    }

    versionCatalogs {
        libs {
            library('nordic-ble-common', 'no.nordicsemi.android:ble-common:2.4.1')
            library('nordic-ble-ktx', 'no.nordicsemi.android:ble-ktx:2.4.1')
            library('nordic-scanner', 'no.nordicsemi.android.support.v18:scanner:1.6.0')

            library('nordic-log', 'no.nordicsemi.android:log:2.3.0')

            version('commonlibraries', '1.0.13')
            library('nordic-ui-scanner', 'no.nordicsemi.android.common', 'uiscanner').versionRef('commonlibraries')
            library('nordic-navigation', 'no.nordicsemi.android.common', 'navigation').versionRef('commonlibraries')
            library('nordic-theme', 'no.nordicsemi.android.common', 'theme').versionRef('commonlibraries')
            library('nordic-analytics', 'no.nordicsemi.android.common', 'analytics').versionRef('commonlibraries')
            library('nordic-ui-logger', 'no.nordicsemi.android.common', 'uilogger').versionRef('commonlibraries')

            library('localbroadcastmanager', 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0')
            library('material', 'com.google.android.material:material:1.6.0-rc01')

            version('lifecycle', '2.4.1')
            library('lifecycle-activity', 'androidx.lifecycle', 'lifecycle-runtime-ktx').versionRef('lifecycle')
            library('lifecycle-service', 'androidx.lifecycle', 'lifecycle-service').versionRef('lifecycle')
            library('compose-lifecycle', 'androidx.lifecycle', 'lifecycle-viewmodel-compose').versionRef('lifecycle')

            library('androidx-core', 'androidx.core:core-ktx:1.7.0')
            library('compose-activity', 'androidx.activity:activity-compose:1.4.0')

            version('datastore', '1.0.0')
            library('datastore-core', 'androidx.datastore', 'datastore').versionRef('datastore')
            library('datastore-prefs', 'androidx.datastore', 'datastore-preferences').versionRef('datastore')
            library('datastore-protobuf', 'com.google.protobuf:protobuf-javalite:3.18.0')
            bundle('datastore', ['datastore-core', 'datastore-prefs', 'datastore-protobuf'])

            version('compose', '1.2.0-alpha07')
            library('compose-ui', 'androidx.compose.ui', 'ui').versionRef('compose')
            library('compose-material', 'androidx.compose.material3:material3:1.0.0-alpha09')
            library('compose-tooling-preview', 'androidx.compose.ui', 'ui-tooling-preview').versionRef('compose')
            library('compose-navigation', 'androidx.navigation:navigation-compose:2.4.1')
            bundle('compose', ['compose-ui', 'compose-material', 'compose-tooling-preview', 'compose-navigation'])

            library('material-icons', 'androidx.compose.material', 'material-icons-core').versionRef('compose')
            library('material-icons-extended', 'androidx.compose.material', 'material-icons-extended').versionRef('compose')
            bundle('icons', ['material-icons', 'material-icons-extended'])

            library('hilt-android', 'com.google.dagger:hilt-android:2.40.4')
            library('hilt-compiler', 'com.google.dagger:hilt-compiler:2.40.4')
            library('hilt-compose', 'androidx.hilt:hilt-navigation-compose:1.0.0')
            library('hilt-lifecycle', 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03')
            library('hilt-lifecyclecompiler', 'androidx.hilt:hilt-compiler:1.0.0')
            bundle('hilt', ['hilt-android', 'hilt-compose', 'hilt-lifecycle'])
            bundle('hiltkapt', ['hilt-compiler', 'hilt-lifecyclecompiler'])

            library('kotlin-coroutines', 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.0')
            library('google-permissions', 'com.google.accompanist:accompanist-permissions:0.20.0')
            library('chart', 'com.github.PhilJay:MPAndroidChart:v3.1.0')

            version('room', '2.4.1')
            library('room-runtime', 'androidx.room', 'room-runtime').versionRef('room')
            library('room-ktx', 'androidx.room', 'room-ktx').versionRef('room')
            library('room-compiler', 'androidx.room', 'room-compiler').versionRef('room')

            //-- Test ------------------------------------------------------------------------------
            library('test-junit', 'junit:junit:4.13.2')
            library('android-test-junit', 'androidx.test.ext:junit:1.1.3')
            library('android-test-espresso', 'androidx.test.espresso:espresso-core:3.4.0')
            library('android-test-compose-ui', 'androidx.compose.ui', 'ui-test-junit4').versionRef('compose')
            library('android-test-compose-tooling', 'androidx.compose.ui', 'ui-tooling').versionRef('compose')

            library('mockk-main', 'io.mockk:mockk:1.12.2')
            library('mockk-jvm', 'io.mockk:mockk-agent-jvm:1.12.2')
            library('test-coroutines', 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0')
            bundle('test', ['test-junit', 'android-test-junit', 'mockk-main', 'mockk-jvm', 'test-coroutines'])
        }
    }
}

rootProject.name = "Android-nRF-Toolbox"

include ':app'

include ':profile_bps'
include ':profile_cgms'
include ':profile_csc'
include ':profile_gls'
include ':profile_hrs'
include ':profile_hts'
include ':profile_prx'
include ':profile_rscs'
include ':profile_uart'

include ':lib_analytics'
include ':lib_service'
include ':lib_ui'
include ':lib_utils'

if (file('../Android-Common-Libraries').exists()) {
    includeBuild('../Android-Common-Libraries')
}

if (file('../Android-BLE-Library').exists()) {
    includeBuild('../Android-BLE-Library')
}
