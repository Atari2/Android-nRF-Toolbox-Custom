plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.firebase.crashlytics'
}
apply from: rootProject.file("gradle/git-tag-version.gradle")

android {
    namespace 'no.nordicsemi.android.nrftoolbox'

    compileSdk android_api_version

    defaultConfig {
        applicationId "no.nordicsemi.android.nrftoolbox"
        minSdk android_min_api_version
        targetSdk android_api_version
        versionCode getVersionCodeFromTags()
        versionName getVersionNameFromTags()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        release {
            storeFile file('../keystore')
            storePassword System.env.KEYSTORE_PSWD
            keyAlias System.env.KEYSTORE_ALIAS
            keyPassword System.env.KEYSTORE_KEY_PSWD
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    hilt {
        enableExperimentalClasspathAggregation = true
    }
}

dependencies {
    //Hilt requires to implement every module in the main app module
    //https://github.com/google/dagger/issues/2123
    implementation project(':profile_bps')
    implementation project(':profile_csc')
    implementation project(':profile_cgms')
    implementation project(':profile_gls')
    implementation project(':profile_hrs')
    implementation project(':profile_hts')
    implementation project(':profile_prx')
    implementation project(':profile_rscs')

    implementation project(':profile_uart')

    implementation project(":lib_analytics")
    implementation project(":lib_theme")
    implementation project(":lib_utils")
    implementation project(":lib_service")
    implementation project(":lib_log")

    implementation libs.nordic.theme
    implementation libs.nordic.ble.common
    implementation libs.nordic.ui.scanner
    implementation libs.nordic.navigation

    implementation libs.bundles.hilt
    kapt libs.bundles.hiltkapt

    implementation libs.bundles.icons
    implementation libs.bundles.compose
    implementation libs.androidx.core
    implementation libs.material
    implementation libs.lifecycle.activity
    implementation libs.compose.lifecycle
    implementation libs.compose.activity

    testImplementation libs.test.junit
    androidTestImplementation libs.android.test.junit
    androidTestImplementation libs.android.test.espresso
    androidTestImplementation libs.android.test.compose.ui
    debugImplementation libs.android.test.compose.tooling
}
